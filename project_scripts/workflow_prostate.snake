#######################################
#
# Snakemake workflow analyze genomes
#
# How to run:
#
# conda create -n cfDNA_project -y snakemake snakemake-executor-plugin-slurm numpy
# conda activate cfDNA_project
# snakemake -s workflow_prostate.snake --executor slurm -j 20 --use-conda --default-resources slurm_account=Fragmentomics --group-components sort_dhs=50 prep_dhs=50 sort_frag=50 prep_frag=50 lwps=50 fdi=50 ifs=50 pfe=50 ocf=50
#
#######################################
import glob
import yaml

with open('config.yaml', 'r') as f:
    config = yaml.safe_load(f)

INPUT_DIR = config['input_dir']
INPUT_DHS_DIR = config['input_dhs_dir']
RESULT_DIR = config['result_dir']
RESULT_SORTED_DIR = config['result_sorted_dir']
MATRIX_COLUMNS = config['matrix_columns']
EXAMPLE_DHS = config['example_dhs']
EXAMPLE_SAMPLE = config['example_sample']
RESULT_PCA_DIR = config['result_pca_dir']

SAMPLES = [f.split('/')[-1].replace('.frag.tsv.gz', '') for f in glob.glob(f"{INPUT_DIR}*.frag.tsv.gz", recursive=True)]
DHS_FILES = [f.split('/')[-1].replace('.bed', '') for f in glob.glob(f"{INPUT_DHS_DIR}*.bed", recursive=True)]
STATS = ['pfe', 'lwps', 'ifs', 'fdi', 'ocf']
STATS_WITHOUT_PFE = [s for s in STATS if s != 'pfe']

rule all:
    input:
        # L-WPS
        expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_lwps.npy", sample=SAMPLES, dhs_file=DHS_FILES),
        
        # FDI
        expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_fdi.npz", sample=SAMPLES, dhs_file=DHS_FILES),
        
        # IFS
        expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_ifs.npz", sample=SAMPLES, dhs_file=DHS_FILES),
        
        # PFE
        expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_pfe.npz", sample=SAMPLES, dhs_file=DHS_FILES),
        
        # OCF
        expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_ocf.npy", sample=SAMPLES, dhs_file=DHS_FILES),
        
        # generate test statistics distribution for an example sample
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_lwps.png",
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_fdi.png",
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_ifs.png",
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_pfe.png",
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_ocf.png",
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.coverage_plot.png",
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.fragment_lengths_plot.png",
        
        expand(f"{RESULT_DIR}feature_matrix_{{stat}}.parquet", stat=STATS),
        expand(f"{RESULT_DIR}{{stat}}_pca_loadings.csv", stat=STATS_WITHOUT_PFE),

        
rule check_dhs_data_sorting:
    input:
        dhs=f"{INPUT_DHS_DIR}{{dhs_file}}.bed"
    output:
        dhs_sorted=f"{RESULT_SORTED_DIR}{{dhs_file}}_sorted.bed"
    resources:
        runtime=5,
        mem_mb=100
    group: "sort_dhs"
    shell:
        '''
        if cat {input.dhs} | awk -F'\t' '{{if(NR>1 && prev > $2 && $1 == prev_chr) {{print "PROBLEM"; exit 1}}; prev=$2; prev_chr=$1}}'; then
            # the file is sorted, only create the symlink
            ln -s $(realpath {input.dhs}) {output.dhs_sorted}
        else
            # the file isn't sorted, so we need to sort it by chr (with -V) and start position (with -n) and then gzip back
            sort -k1,1V -k2,2n {input.dhs} > {output.dhs_sorted}
        fi
        '''
        
rule preprocess_dhs_sorted_data:
    input:
        dhs_sorted=f"{RESULT_SORTED_DIR}{{dhs_file}}_sorted.bed"
    output:
        dhs_sorted_preprocessed=f"{RESULT_SORTED_DIR}{{dhs_file}}_sorted_wl{MATRIX_COLUMNS}.bed"
    resources:
        runtime=30,
        mem_mb=300
    group: "prep_dhs"
    script:
        "preprocess_dhs.py"
        
rule check_fragments_data_sorting:
    input:
        fragment=f"{INPUT_DIR}{{sample}}.frag.tsv.gz",
    output:
        fragment_sorted=f"{RESULT_SORTED_DIR}{{sample}}_sorted.frag.tsv.gz"
    resources:
        runtime=5,
        mem_mb=100
    group: "sort_frag"
    shell:
        '''
        if zcat {input.fragment} | awk -F'\t' '{{if(NR>1 && prev > $2 && $1 == prev_chr) {{print "PROBLEM"; exit 1}}; prev=$2; prev_chr=$1}}'; then
            # the file is sorted, only create the symlink
            ln -s $(realpath {input.fragment}) {output.fragment_sorted}
        else
            # the file isn't sorted, so we need to sort it by chr (with -V) and start position (with -n) and then gzip back
            zcat {input.fragment} | sort -k1,1V -k2,2n | gzip -c > {output.fragment_sorted}
        fi
        '''

rule preprocess_fragments:
    input:
        fragment=f"{RESULT_SORTED_DIR}{{sample}}_sorted.frag.tsv.gz",
        #dhs=f"{RESULT_SORTED_DIR}{{dhs_file}}_sorted.bed"
        dhs=f"{RESULT_SORTED_DIR}{{dhs_file}}_sorted_wl{MATRIX_COLUMNS}.bed"
    output:
        f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted.npy"
    resources:
        runtime=600,
        mem_mb=100
    group: "prep_frag"
    script:
        "preprocess_fragments.py"
        
# LWPS calculation
rule calculate_lwps:
    input:
        matrix=f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted.npy",
        config="config.yaml"
    output:
        f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_lwps.npy"
    params:
        statistic="lwps"
    resources:
        mem_mb=200,
        runtime=60
    group: "lwps"
    script:
        "calculate_statistics.py"

# FDI calculation
rule calculate_fdi:
    input:
        matrix=f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted.npy",
        config="config.yaml"
    output:
        f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_fdi.npz"
    params:
        statistic="fdi"
    resources:
        mem_mb=300,
        runtime=60
    group: "fdi"
    script:
        "calculate_statistics.py"
        
# IFS calculation
rule calculate_ifs:
    input:
        matrix=f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted.npy",
        config="config.yaml"
    output:
        f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_ifs.npz"
    params:
        statistic="ifs"
    resources:
        mem_mb=200,
        runtime=60
    group: "ifs"
    script:
        "calculate_statistics.py"

# PFE calculation
rule calculate_pfe:
    input:
        matrix=f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted.npy",
        config="config.yaml"
    output:
        f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_pfe.npz"
    params:
        statistic="pfe"
    resources:
        mem_mb=200,
        runtime=60
    group: "pfe"
    script:
        "calculate_statistics.py"
        
# OCF calculation
rule calculate_ocf:
    input:
        matrix=f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted.npy",
        config="config.yaml"
    output:
        f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_ocf.npy"
    params:
        statistic="ocf"
    resources:
        mem_mb=200,
        runtime=60
    group: "ocf"
    script:
        "calculate_statistics.py"
        
# L-WPS visualization
rule visualize_lwps:
    input:
        matrix=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.npy",
        statistic=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_lwps.npy",
        config="config.yaml"
    output:
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_lwps.png"
    params:
        statistic="lwps",
        sample=EXAMPLE_SAMPLE
    script:
        "visualize_statistics.py"

# FDI visualization
rule visualize_fdi:
    input:
        matrix=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.npy",
        statistic=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_fdi.npz",
        config="config.yaml"
    output:
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_fdi.png"
    params:
        statistic="fdi",
        sample=EXAMPLE_SAMPLE
    script:
        "visualize_statistics.py"
        
# IFS visualization
rule visualize_ifs:
    input:
        matrix=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.npy",
        statistic=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_ifs.npz",
        config="config.yaml"
    output:
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_ifs.png"
    params:
        statistic="ifs",
        sample=EXAMPLE_SAMPLE
    script:
        "visualize_statistics.py"

# PFE visualization
rule visualize_pfe:
    input:
        matrix=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.npy",
        statistic=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_pfe.npz",
        config="config.yaml"
    output:
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_pfe.png"
    params:
        statistic="pfe",
        sample=EXAMPLE_SAMPLE
    script:
        "visualize_statistics.py"
        
# OCF visualization
rule visualize_ocf:
    input:
        matrix=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.npy",
        statistic=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_ocf.npy",
        config="config.yaml"
    output:
        f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted_ocf.png"
    params:
        statistic="ocf",
        sample=EXAMPLE_SAMPLE
    script:
        "visualize_statistics.py"
        
# General visualization
rule visualize_matrix:
    input:
        matrix=f"{RESULT_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.npy",
        config="config.yaml"
    output:
        coverage_plot=f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.coverage_plot.png",
        fragment_lengths_plot=f"{RESULT_PCA_DIR}{EXAMPLE_SAMPLE}__{EXAMPLE_DHS}_sorted.fragment_lengths_plot.png",
    script:
        "visualize_matrix.py"
        
# merge same test statistics 
rule build_feature_matrices:
    input:
        lwps_inputs=expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_lwps.npy",
                           sample=SAMPLES, dhs_file=DHS_FILES),
        ocf_inputs=expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_ocf.npy",
                          sample=SAMPLES, dhs_file=DHS_FILES),
        fdi_inputs=expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_fdi.npz",
                          sample=SAMPLES, dhs_file=DHS_FILES),
        ifs_inputs=expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_ifs.npz",
                          sample=SAMPLES, dhs_file=DHS_FILES),
        pfe_inputs=expand(f"{RESULT_DIR}{{sample}}__{{dhs_file}}_sorted_pfe.npz",
                          sample=SAMPLES, dhs_file=DHS_FILES),      
        config="config.yaml",
    params:
        dhs_files=DHS_FILES,
    output:
        matrices=expand(f"{RESULT_DIR}feature_matrix_{{stat}}.parquet", stat=STATS),
        loadings=expand(f"{RESULT_DIR}{{stat}}_pca_loadings.csv", stat=STATS_WITHOUT_PFE),
    resources:
        runtime=60,
        mem_mb=8000
    script:
        "build_matrices_joint.py"